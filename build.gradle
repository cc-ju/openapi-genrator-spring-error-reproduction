plugins {
    id 'org.springframework.boot' version '2.7.0'
    id 'org.openapi.generator' version '6.0.0'
    //id 'org.openapi.generator' version '5.3.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // generator 6.x
    implementation 'io.swagger.core.v3:swagger-annotations:2.2.0'
    // generator 5.x
    //implementation 'io.swagger:swagger-annotations:1.6.6'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

openApiGenerate {
    inputSpec = "$rootDir/openapi.yaml"
    generatorName = "spring"
    validateSpec = true
    apiPackage = "com.example.demo.api"
    modelPackage = "com.example.demo.api.model"
    outputDir = "$buildDir/generated-sources"
    library = "spring-boot"
    configOptions = [
            "interfaceOnly": "true"
    ]
    globalProperties = [
            "skipFormModel": "false"
    ]
}

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated-sources/src/main/java"
        }
    }
}

tasks.withType(JavaCompile) {
    dependsOn(tasks.openApiGenerate)
}
tasks.named('test') {
    useJUnitPlatform()
}
